/*
 * Copyright (c) 2019 Adyen N.V.
 *
 * This file is open source and available under the MIT license. See the LICENSE file for more info.
 *
 * Created by caiof on 8/2/2019.
 */

plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.parcelize
}

// Maven artifact
ext.mavenArtifactId = "3ds2"
ext.mavenArtifactName = "Adyen checkout 3DS2 component"
ext.mavenArtifactDescription = "Adyen Checkout 3DS2 component client for Adyen's Checkout API."

apply from: "${rootDir}/config/gradle/sharedTasks.gradle"

android {
    namespace 'com.adyen.checkout.adyen3ds2'
    compileSdk libs.versions.compile.sdk.get().toInteger()

    defaultConfig {
        minSdk libs.versions.min.sdk.get().toInteger()
        targetSdk libs.versions.target.sdk.get().toInteger()

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles "consumer-rules.pro"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    // Checkout
    api project(':ui-core')

    // Dependencies
    api libs.adyen3ds2

    //Tests
    testImplementation testFixtures(project(':test-core'))
    testImplementation testFixtures(project(':components-core'))
    testImplementation testFixtures(project(':ui-core'))
    testImplementation libs.json
    testImplementation libs.bundles.junit
    testImplementation libs.bundles.mockito
    testImplementation libs.bundles.kotlin.coroutines.test
}
