/*
 * Copyright (c) 2019 Adyen N.V.
 *
 * This file is open source and available under the MIT license. See the LICENSE file for more info.
 *
 * Created by caiof on 8/2/2019.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'dagger.hilt.android.plugin'
}

apply from: "${rootDir}/config/gradle/codeQuality.gradle"

if (file("local.gradle").exists()) {
    apply from: "local.gradle"
} else if (System.getenv("CI") == "true") {
    apply from: "ci.local.gradle"
} else if (!isIDEBuild()) {
    // Renovate doesn't set the CI variable, so this way we can still make sure the build succeeds.
    apply from: "ci.local.gradle"
} else {
    throw new GradleException("File example-app/local.gradle not found. Check example-app/README.md for more instructions.")
}

def isIDEBuild() {
    return project.properties['android.injected.invoked.from.ide'] == 'true'
}

android {
    compileSdk compile_sdk_version

    defaultConfig {
        applicationId "com.adyen.checkout.example"
        namespace "com.adyen.checkout.example"

        minSdk min_sdk_version
        targetSdk target_sdk_version
        versionCode version_code
        versionName version_name

        testInstrumentationRunner 'com.adyen.checkout.HiltTestRunner'
    }

    testOptions {
        animationsDisabled = true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
        viewBinding true
        buildConfig true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = compose_compiler_version
    }
}

dependencies {
    // Checkout
    implementation project(':drop-in')
    implementation project(':components-compose')
//    implementation "com.adyen.checkout:drop-in:5.3.1"
//    implementation "com.adyen.checkout:components-compose:5.3.1"

    // Dependencies
    implementation libraries.kotlinCoroutines

    implementation libraries.androidx.appcompat
    implementation libraries.androidx.recyclerview
    implementation libraries.androidx.constraintlayout
    implementation libraries.androidx.preference

    implementation platform(libraries.compose.bom)
    implementation libraries.compose.ui
    implementation libraries.compose.activity
    implementation libraries.compose.hilt
    implementation libraries.compose.material
    implementation libraries.compose.viewmodel

    implementation libraries.material

    implementation libraries.retrofit
    implementation libraries.moshi
    implementation libraries.okhttpLogging

    implementation libraries.hilt
    ksp libraries.hiltCompiler

    implementation libraries.googlePay.composeButton

    debugImplementation libraries.leakCanary
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    // Tests
    testImplementation testLibraries.junit5
    testImplementation testLibraries.mockito

    androidTestImplementation testLibraries.androidTest
    androidTestImplementation testLibraries.barista
    androidTestImplementation testLibraries.espresso
    androidTestImplementation testLibraries.hilt

    kspAndroidTest testLibraries.hiltCompiler
}
