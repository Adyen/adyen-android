apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.adyen.checkout"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode rootProject.versionCode
        versionName rootProject.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "SERVER_URL", "\"" + System.getProperty("SERVER_URL", "").trim() + "\""
        buildConfigField "String", "API_KEY", "\"" + System.getProperty("API_KEY", "").trim() + "\""
        buildConfigField "String", "API_HEADER_KEY", "\"" + System.getProperty("API_HEADER_KEY", "").trim() + "\""
    }
    buildTypes {
        release {
            debuggable rootProject.release_debuggable
            minifyEnabled rootProject.release_minifyEnabled
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable rootProject.debug_debuggable
            testCoverageEnabled = rootProject.debug_testCoverageEnabled
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }

    testBuildType rootProject.testBuildType
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile "junit:junit:${rootProject.jUnitVersion}"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'

    // UiAutomator
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile project(':testutils')

    compile "com.android.support:support-annotations:${rootProject.supportAnnotationsVersion}"
    if (publishNonDefault) {
        debugCompile project(path: ':adyen-core', configuration: 'debug')
        releaseCompile project(path: ':adyen-core', configuration: 'release')

        debugCompile project(path: ':adyen-androidpay', configuration: 'debug')
        releaseCompile project(path: ':adyen-androidpay', configuration: 'release')

        debugCompile project(path: ':adyen-ui', configuration: 'debug')
        releaseCompile project(path: ':adyen-ui', configuration: 'release')
    } else {
        compile project(':adyen-core')
        compile project(':adyen-androidpay')
        compile project(':adyen-ui')
    }
}