apply plugin: 'jacoco'

if (project.hasProperty('android')) {
    android.buildTypes {
        debug {
            testCoverageEnabled = true
        }
    }
}

project.afterEvaluate {
    jacoco {
        toolVersion = "0.8.9"
    }

    tasks.withType(Test).configureEach {
        jacoco {
            excludes += coverageExclusions
            includeNoLocationClasses = true
        }
    }

    if (project.hasProperty('android')) {
        tasks.register('jacocoTestReport', JacocoReport) {
            group 'Reporting'
            description 'Generate JaCoCo report for debug unit tests'
            dependsOn 'testDebugUnitTest'

            reports {
                xml.required = true
                html.required = false
                csv.required = false
            }

            additionalSourceDirs(android.sourceSets.main.java.sourceFiles)
            additionalSourceDirs(android.sourceSets.debug.java.sourceFiles)
            additionalClassDirs(fileTree(dir: "${layout.buildDirectory.get().asFile}/intermediates/javac/debug", excludes: coverageExclusions))
            additionalClassDirs(fileTree(dir: "${layout.buildDirectory.get().asFile}/tmp/kotlin-classes/debug", excludes: coverageExclusions))
            executionData(
                fileTree(dir: "${layout.buildDirectory.get().asFile}", includes: [
                    "outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec",
                    "jacoco/test.exec",
                    "outputs/code-coverage/connected/*coverage.ec"
                ]),
                fileTree(dir: "$projectDir", includes: ['jacoco.exec'])
            )
        }
    }
}

ext.coverageExclusions = [
    // Android
    '**/BuildConfig.*',
    '**/Manifest*.*',
    '**/R$*.class',
    '**/R.class',

    // Data binding
    '**/*BindingAdapter.*',
    '**/*BindingAdapters.*',
    '**/*ViewBinding.*',
    '**/BR$*.class',
    '**/BR.class',
    '**/DataBinderMapperImpl*.*',
    '**/DataBinderMapperImpl.*',
    '**/databinding/**',

    // Activities, Fragments, etc. (not tested with unit tests)
    '**/*Activity$*.*',
    '**/*Activity.*',
    '**/*Adapter$*.*',
    '**/*Adapter.*',
    '**/*Behavior.*',
    '**/*Dialog.*',
    '**/*Drawable.*',
    '**/*Fragment$*.*',
    '**/*Fragment.*',
    '**/*View$*.*',
    '**/*View.*',
    '**/*ViewHolder$*.*',
    '**/*ViewHolder.*',

    // Parcelize
    '**/*$Creator',
    '**/*$Creator*.*',

    // Activity result contract
    '**/*ActivityResults.*',
    '**/*ResultContract.*',

    // Dagger + Hilt
    '**/*_ComponentTreeDeps.*',
    '**/*_Factory.*',
    '**/*_GeneratedInjector.*',
    '**/*_HiltComponents.*',
    '**/*_HiltComponents_*.*',
    '**/*_HiltModules*.*',
    '**/*_HiltModules.*',
    '**/*_HiltModules_*.*',
    '**/*_Member*Injector.*',
    '**/*_Provide*Factory*.*',
    '**/*_Provide*Factory.*',
    '**/*_ProvideFactory.*',
    '**/Dagger*.*',
    '**/Hilt_*.*',
    '**/dagger/**',
    '**/hilt_aggregated_deps/**',

    // Test classes
    '**/*Test.*',
    '**/Test*.*',

    // Fix issue with JaCoCo on JDK
    'jdk.internal.*',

    // Custom views not following *View naming
    '**/AddressFormInput*.*',
    '**/AdyenSwipeToRevealLayout*.*',
    '**/AdyenTextInputEditText*.*',
    '**/CardNumberInput.*',
    '**/DefaultPayButton.*',
    '**/ExpiryDateInput.*',
    '**/GiftCardNumberInput.*',
    '**/IbanInput.*',
    '**/PayButton.*',
    '**/SecurityCodeInput.*',
    '**/SocialSecurityNumberInput.*',

    // Project specific files that can't be unit tested
    '**/*ComponentProvider$*.*',
    '**/*ComponentProvider.*',
    '**/*DropInService$*.*',
    '**/*DropInService.*',
    '**/*Factory.*',
    '**/*ViewProvider.*',
    '**/AdyenLogger.*',
    '**/BuildUtils.*',
    '**/ContextExtensions*.*',
    '**/DropInExt*.*',
    '**/FileDownloader*.*',
    '**/FragmentExtensions*.*',
    '**/ImageLoadingExtensions*.*',
    '**/ImageLoadingExtensions.*',
    '**/ImageSaver.*',
    '**/InstallmentFilter.*',
    '**/LazyArguments*.*',
    '**/LifecycleExtensions*.*',
    '**/LogUtil.*',
    '**/Logger.*',
    '**/PdfOpener.*',
    '**/ViewExtensions*.*',
    '**/ViewModelExt*.*',

    // Example app is not applicable
    'com/adyen/checkout/example/**'
]
