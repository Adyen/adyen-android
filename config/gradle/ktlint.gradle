/*
 * Copyright (c) 2019 Adyen N.V.
 *
 * This file is open source and available under the MIT license. See the LICENSE file for more info.
 *
 * Created by ran on 23/5/2019.
 */

/**
 * An anti-bikeshedding Kotlin linter with built-in formatter.
 *
 * Docs: https://github.com/pinterest/ktlint
 */

configurations {
    ktlint
}

dependencies {
    ktlint libs.ktlint.cli
}

tasks.register("ktlint", JavaExec) {
    description = "Check Kotlin code style."
    group = "verification"

    classpath = configurations.ktlint
    mainClass = "com.pinterest.ktlint.Main"
    args "src/**/main/**/*.kt", "**.kts", "!**/build/**"
}

tasks.named("check") {
    dependsOn tasks.named("ktlint")
}

tasks.register("ktlintFormat", JavaExec) {
    description = "Fix Kotlin code style deviations."
    group = "formatting"

    classpath = configurations.ktlint
    mainClass = "com.pinterest.ktlint.Main"
    jvmArgs "--add-opens=java.base/java.lang=ALL-UNNAMED"
    args "-F", "src/**/*.kt", "**.kts", "!**/build/**"
}
