buildscript {
    apply from: './dependencies.gradle'
}

plugins {
    id 'com.android.application' version "$android_gradle_plugin_version" apply false
    id 'com.android.library' version "$android_gradle_plugin_version" apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'com.google.dagger.hilt.android' version "$hilt_version" apply false
    id "io.gitlab.arturbosch.detekt" version "$detekt_gradle_plugin_version"
    id "org.jetbrains.dokka" version "$dokka_version"
}

apply from: "config/gradle/dokkaRoot.gradle"

ext {
    checkoutRedirectScheme = "adyencheckout"
}

allprojects {
    tasks.withType(Test) {
        useJUnitPlatform()
    }
}

subprojects {
    project.afterEvaluate {
        def javaVersion = 11
        def isAndroid = plugins.hasPlugin("com.android.application") || plugins.hasPlugin("com.android.library")
        if (isAndroid) {
            android {
                compileOptions {
                    sourceCompatibility = javaVersion
                    targetCompatibility = javaVersion
                }
            }
        }

        if (plugins.hasPlugin("kotlin-android")) {
            kotlin {
                jvmToolchain(javaVersion)
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += [
                '-opt-in=kotlin.RequiresOptIn'
            ]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: file('config/gradle/dependencyGraph.gradle')
