apply from: "config/gradle/checksums.gradle"
apply from: "config/gradle/dependenciesCheck.gradle"

ext {
    checkoutRedirectScheme = "adyencheckout"
}

buildscript {
    apply from: './dependencies.gradle'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_gradle_plugin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_gradle_plugin_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

apply from: "config/gradle/dokkaRoot.gradle"

allprojects {
    repositories {
        google()
        mavenCentral()
    }

    tasks.withType(Test) {
        useJUnitPlatform()
    }
}

subprojects {
    project.afterEvaluate {
        def javaVersion = 11
        def isAndroid = plugins.hasPlugin("com.android.application") || plugins.hasPlugin("com.android.library")
        if (isAndroid) {
            android {
                compileOptions {
                    sourceCompatibility = javaVersion
                    targetCompatibility = javaVersion
                }
            }
        }

        if (plugins.hasPlugin("kotlin-android")) {
            kotlin {
                jvmToolchain(javaVersion)
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += [
                '-opt-in=kotlin.RequiresOptIn'
            ]
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: file('config/gradle/dependencyGraph.gradle')
